apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

def openCVersionName = "4.12.0"
def openCVersionCode = ((4 * 100 + 12) * 100 + 0) * 10 + 0

android {
    namespace 'org.opencv'
    compileSdkVersion 36

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode openCVersionCode
        versionName openCVersionName

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
                targets "opencv_jni_shared"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlin {
        jvmToolchain(17)
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            packagingOptions {
                doNotStrip '**/*.so'
            }
        }
        debug {
            packagingOptions {
                doNotStrip '**/*.so'
            }
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['native/libs']
            java.srcDirs = ['java/src']
            res.srcDirs = ['java/res']
            manifest.srcFile 'java/AndroidManifest.xml'
        }
    }

    externalNativeBuild {
        cmake {
            path "${project.projectDir}/libcxx_helper/CMakeLists.txt"
        }
    }

    buildFeatures {
        prefabPublishing true
        buildConfig true
    }

    prefab {
        opencv_jni_shared {
            headers 'native/jni/include'
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'org.opencv'
            artifactId = 'opencv'
            version = openCVersionName
            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        maven {
            name = 'myrepo'
            url = "${project.buildDir}/repo"
        }
    }
}

dependencies {
}
